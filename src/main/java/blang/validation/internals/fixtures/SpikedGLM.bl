package blang.validation.internals.fixtures

import static extension blang.validation.internals.fixtures.SpikedRealVar.*


model SpikedGLM {
  
  param Matrix designMatrix // n by p
  random List<IntVar> output // size n
  random List<SpikedRealVar> coefficients ?: {
    val p = designMatrix.nCols
    return new ArrayList(p) => [
      for (int i : 0 ..< p) 
        add(new SpikedRealVar)
    ]
  } 
  
  random RealVar zeroProbability ?: realVar(0.5)
  
  laws {
    
    zeroProbability ~ Beta(1,1)
    
    coefficients | zeroProbability ~ 
      SpikeAndSlab(zeroProbability, Normal::distribution(0, 1)) 
      // Weird explicit import because of workaround for Issue #56
    
    for (int index : 0 ..< output.size) {
      output.get(index) | coefficients, 
                          Matrix predictors = designMatrix.row(index) 
                        ~ Bernoulli(logistic(coefficients * predictors))
    }
    
  }
  
}