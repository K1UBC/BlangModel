package blang.distributions

model MultivariateNormal {
  
  random Matrix realization
  
  param Matrix mean
  param CholeskyDecomposition precision
  
  laws {
    
    logf(double dim = realization.nEntries) {
      - dim * log(Math.sqrt(2*Math.PI)) / 2.0
    }
    
    logf(precision) {
      0.5 * precision.logDet
    }
    
    logf(mean, precision, realization)  {
      val Matrix centered = mean - realization
      val Matrix L = precision.L
      // Note: caching L L' would not change the asymptotic running time of d^2
      return - 0.5 * (centered.transpose * L * L.transpose * centered).doubleValue
    }
    
  }
  
  generate(rand) {
    val DenseMatrix newSample = sampleNormalByPrecision(rand, precision)
    realization *= 0.0
    realization += newSample
  }
  
}