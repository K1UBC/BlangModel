package blang.distributions

import blang.distributions.internals.Helpers

model Dirichlet {
  random Simplex realization 
  param  Matrix concentrations 
  
  laws {
    logf(concentrations, realization) {
      var sum = 0.0
      for (int dim : 0 ..< concentrations.nEntries) {
        val concentration = concentrations.get(dim)
        if (concentration < 0.0) return NEGATIVE_INFINITY
        Helpers::checkDirichletOrBetaParam(concentration)
        sum += (concentration - 1.0) * log(realization.get(dim))
      }
      return sum
    }
    logf(concentrations) {
      var sum = 0.0
      for (int dim : 0 ..< concentrations.nEntries) {
        val concentration = concentrations.get(dim)
        if (concentration < 0.0) return NEGATIVE_INFINITY
        sum += - lnGamma(concentration)
      }
      return sum + lnGamma(concentrations.sum) 
    }
    realization is Constrained
  }
  
  generate(rand) { 
    rand.dirichletInPlace(concentrations, realization) 
  }
}
