package blang.distributions

model Dirichlet {
  
  random Simplex realization 
  
  param  Matrix concentrations 
  
  laws {
    
    for (int dim : 0 ..< concentrations.nEntries) {
      logf(
        RealVar concentration = concentrations.getRealVar(dim),
        RealVar x = realization.getRealVar(dim)
      ) {
        check(concentration > 0)
        (concentration - 1.0) * log(x)
      }
      
      logf(RealVar concentration = concentrations.getRealVar(dim)) {
        - lnGamma(concentration)
      }
    }
    
    logf(concentrations) {
      lnGamma(concentrations.sum) 
    }
    
    realization ~ new ConstrainedFactor()
  }
  
  generate(rand) { 
    var double sum = 0.0
    for (var int d = 0; d < concentrations.nEntries; d++)
    {
      val double gammaVariate = Generators::gamma(rand, concentrations.get(d), 1.0)
      realization.set(d, gammaVariate)
      sum += gammaVariate
    }
    realization /= sum
  }
}