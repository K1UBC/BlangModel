package blang.examples

import blang.types.Simplex
import blang.types.TransitionMatrix
import java.util.List


model Texting {
  
  random List<IntVar>     data
  random List<IntVar>     chain         ?: listOfIntVars(data.size)
  
  random List<RealVar>    means         ?: listOfRealVars(2)
  
  param  Simplex          initialDist   ?: simplex(2)
  param  TransitionMatrix transMtx      ?: 
    transitionMatrix(#[
      #[0.99, 0.01],
      #[0.01, 0.99]
    ])   
  
  param  RealVar          shape         ?: 1.0, 
                          rate          ?: 1.0
  
  laws {
    
    for (int latentIdx : 0 .. 1) {
      means.get(latentIdx) | shape, rate ~ Gamma(shape, rate)
    }
    
    chain | initialDist, transMtx ~ MarkovModel(initialDist, transMtx)
    
    for (int obsIdx : 0 ..< data.size) {
      data.get(obsIdx) | IntVar curLatent = chain.get(obsIdx), 
                                means
                       ~ Poisson(means.get(curLatent))
    }
    
  }
}