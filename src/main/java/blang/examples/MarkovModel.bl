package blang.examples

import blang.types.IntVar
import xlinear.Matrix
import java.util.List 
import blang.mcmc.Samplers
import blang.mcmc.RealNaiveMHSampler

import static extension blang.utils.ExtensionUtils.*
import static extension xlinear.MatrixOperations.* 

@Samplers(blang.mcmc.BaumWelch)
model MarkovModel {  
  
  param Matrix initialDistribution      ?: denseCopy(#[0.5, 0.5])
  param Matrix transitionProbabilities  ?: 
    denseCopy(#[
      #[0.99,  0.01],
      #[0.01,  0.99]
    ])
  
  random List<IntVar> chain             ?: listOfIntVars(5)
   
  laws { 
    // Initial distribution:
    chain.get(0) | initialDistribution ~ Categorical(initialDistribution)
    // Transitions: 
    for (int step : 1 ..< chain.size) {
      chain.get(step) |
        IntVar previous = chain.get(step - 1),
        transitionProbabilities
        ~ Categorical(transitionProbabilities.row(previous.intValue)) 
    }
  }
}